/*
 * Copyright 2020 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * This is the root project descriptor for the Google Ads API Java library
 * gradle project.
 */
pluginManagement {
  repositories {
    gradlePluginPortal()
  }
  plugins {
    id 'org.jreleaser' version '1.18.0'
  }
}

rootProject.name = 'google-ads-parent'

include ':google-ads-codegen'
include ':google-ads'
if (startParameter.projectProperties.containsKey("fastbuild")) {
  // Makes absolutely sure that we do not try to enable fastbuild when doing a release.
  if (startParameter.projectProperties.containsKey("release")) {
    throw new IllegalArgumentException(
      'Properties "fastbuild" and "release" are both set. These properties are incompatible.')
  }
  // Skips the inclusion of the shadowjar project if fastbuild is enabled.
  logger.warn('Excluding :google-ads-shadowjar because "fastbuild" property is set')
} else {
  include ':google-ads-shadowjar'
}
include ':google-ads-stubs-lib'

// Enables the examples project only if we're not deploying to Sonatype.
if (!startParameter.projectProperties.containsKey("release")) {
  include ':google-ads-examples'
}

// Keep the stubs modules at the end of this file. The stubs generator will
// append new modules here as required. There should *not* be a newline at EOF.
include ':google-ads-stubs-v18'
include ':google-ads-stubs-v19'
include ':google-ads-stubs-v20'
