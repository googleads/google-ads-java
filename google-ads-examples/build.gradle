/*
 * Copyright 2020 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * This is the build file for the google-ads-examples subproject. It depends on
 * the google-ads subproject.
 *
 * This project contains examples for using the Google Ads API. You can launch
 * examples by navigating to the google-ads-examples folder and running the
 * provided runExample gradle task. Note that we recommend running with the
 * quiet flag (-q) to reduce screen clutter:
 *     -- Mac/Linux --
 *     ./gradlew -q runExample --example='basicoperations.GetCampaigns --customerId 1234567890'
 *
 *     -- Windows --
 *     gradlew -q runExample --example='basicoperations.GetCampaigns --customerId 1234567890'
 */

plugins {
    id 'com.google.api-ads.java-conventions'
}

dependencies {
    implementation project(':google-ads')
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'com.beust:jcommander:1.72'
    implementation 'joda-time:joda-time:2.8.2'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.1'
    testImplementation 'org.mockito:mockito-core:2.27.0'
}

description = 'Google Ads API client library for Java examples'

public class ExampleRunnerTask extends JavaExec {

    private final String basePackage = 'com.google.ads.googleads.examples.'

    @Optional
    @Input
    private String exampleArguments

    public ExampleRunnerTask() {
        group = 'Execution'
        description = 'Run a Google Ads API example.'
        errorOutput = System.err
    }

    @Option(option = 'example', description = 'Sets the example to launch and' +
        ' any arguments. Required for execution. E.g. ' +
        '"basicoperations.GetCampaigns --customerId 1234567890"')
    public void setExampleArguments(String exampleArguments) {
        this.exampleArguments = exampleArguments.trim()
        int firstSpaceIndex = this.exampleArguments.indexOf(' ')

        // No additional arguments were passed, just the example name.
        if (firstSpaceIndex == -1) {
            main = basePackage + this.exampleArguments
        }
        // Otherwise, separate the input and set the arguments to pass to the
        // main class.
        else {
            main = basePackage + this.exampleArguments[0..firstSpaceIndex-1]
            argsString(exampleArguments[(firstSpaceIndex + 1)..-1])
        }
    }

    @TaskAction
    @Override
    public void exec() {
        if (!(exampleArguments?.trim())) {
            throw new GradleException('\033[0;31mMissing example!\033[0m ' +
                'Please rerun with one provided, e.g. ' +
                '\033[0;35m--example="basicoperations.GetCampaigns"\033[0m')
        }
        logQuietMessage('Running example: ' + main + ', args: ' +
            args.toString())
        try {
            super.exec()
        }
        catch (Exception e) {
            logQuietMessage('\n\033[0;31mrunExample exception!\033[0m Did ' +
                'you provide a valid example identifier? E.g. ' +
                '\033[0;35m--example="basicoperations.GetCampaigns"\033[0m\n\n' +
                e.message)
        }
    }

    public String getExampleArguments() {
        return exampleArguments
    }

    private void logQuietMessage(String message) {
        logger.quiet(message)
    }

}

task runExample(type: ExampleRunnerTask) {
    classpath = sourceSets.main.runtimeClasspath
}
